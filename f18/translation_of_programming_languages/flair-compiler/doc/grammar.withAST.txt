
<PROGRAM> ::= program <IDENTIFIER> {make-Identifier} ( <FORMALS> ) ;
                 <DEFINITIONS> {make-Definitions}
                 <BODY> . {make-Program}

<DEFINITIONS> ::= ε
            | <DEF> <DEFINITIONS>

    <DEF> ::= function <IDENTIFIER> {make-Identifier} ( <FORMALS> ) : <TYPE>
                 <BODY> ; {make-Function}

<FORMALS> ::= ε
            | <NONEMPTYFORMALS> {make-Formals}

<NONEMPTYFORMALS> ::= <FORMAL> <NONEMPTYFORMALS'>

<NONEMPTYFORMALS'> ::= ε
            | , <NONEMPTYFORMALS>

 <FORMAL> ::= <IDENTIFIER> {make-Identifier} : <TYPE> {make-Formal}

   <BODY> ::= begin <STATEMENT-LIST> end {make-Body}

<STATEMENT-LIST> ::= <PRINT-STATEMENT> <STATEMENT-LIST>
            | return <EXPR> {make-ReturnStatement}

   <TYPE> ::= integer {make-Type}
            | boolean {make-Type}

   <EXPR> ::= <SIMPLE-EXPR> <EXPR'>

   <EXPR'> ::= ε
            | < <SIMPLE-EXPR> {make-LessThan} <EXPR'>
            | = <SIMPLE-EXPR> {make-EqualTo} <EXPR'>

<SIMPLE-EXPR> ::= <TERM> <SIMPLE-EXPR'>

<SIMPLE-EXPR'> ::= ε
            | or <TERM> {make-OrExpr} <SIMPLE-EXPR'>
            | + <TERM> {make-PlusExpr} <SIMPLE-EXPR'>
            | - <TERM> {make-MinusExpr} <SIMPLE-EXPR'>

   <TERM> ::= <FACTOR> <TERM'>

  <TERM'> ::= ε
            | and <FACTOR> {make-AndExpr} <TERM'>
            | * <FACTOR> {make-TimesExpr} <TERM'>
            | / <FACTOR> {make-DivideExpr} <TERM'>

 <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR> {make-IfStatement}
            | not <FACTOR> {make-NotExpr}
            | <IDENTIFIER> {make-Identifier} <FACTOR'>
            | <LITERAL>
            | - <FACTOR> {make-NegateExpr}
            | ( <EXPR> )

 <FACTOR'> ::= ε
            | ( <ACTUALS> ) {make-FunctionCall}

<ACTUALS> ::= ε
            | <NONEMPTYACTUALS> {make-Actuals}

<NONEMPTYACTUALS> ::= <EXPR> {make-Actual} <NONEMPTYACTUALS'>

<NONEMPTYACTUALS'> ::= ε
            | , <NONEMPTYACTUALS>

<LITERAL> ::= <NUMBER> {make-IntegerLiteral}
            | <BOOLEAN> {make-BooleanLiteral}

<PRINT-STATEMENT> ::= print ( <EXPR> ) ; {make-PrintStatement}
